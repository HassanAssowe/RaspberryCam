apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.hassanassowe.raspberrycam"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 6
        versionName "1.2.5"
        ndkVersion '21.3.6528147'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //Application Dependencies
        vectorDrawables.useSupportLibrary = true

        externalNativeBuild {
            ndkBuild {
                def gstRoot

                if (project.hasProperty('gstAndroidRoot'))
                    gstRoot = project.gstAndroidRoot
                else
                    gstRoot = System.env.GSTREAMER_ROOT_ANDROID

                if (gstRoot == null)
                    throw new GradleException('GSTREAMER_ROOT_ANDROID must be set, or "gstAndroidRoot" must be defined in your gradle.properties in the top level directory of the unpacked universal GStreamer Android binaries')

                arguments "NDK_APPLICATION_MK=jni/Application.mk", "GSTREAMER_JAVA_SRC_DIR=src", "GSTREAMER_ROOT_ANDROID=$gstRoot", "GSTREAMER_ASSETS_DIR=src/assets"

                targets "GStreamer"

                // All archs except MIPS and MIPS64 are supported
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }

    buildTypes {
        //CUSTOM ADDITION, Counts Build #
        def buildCountFile = new File("C:\\Users\\Hassan\\AndroidStudioProjects\\RaspberryCam\\gradle-counter.txt")
        String content = ""
        if (buildCountFile.exists()) {
            content = buildCountFile.getText('UTF-8')
        }
        def count = 0
        if (content.isNumber()) {
            count = content.toInteger() + 1
        }
        buildCountFile.write(count.toString())
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "int", "BUILDNUM", count.toString()
        }
        debug {
            buildConfigField "int", "BUILDNUM", count.toString()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
        }
    }
}

afterEvaluate {
    if (project.hasProperty('compileDebugJavaWithJavac'))
        project.compileDebugJavaWithJavac.dependsOn 'externalNativeBuildDebug'
    if (project.hasProperty('compileReleaseJavaWithJavac'))
        project.compileReleaseJavaWithJavac.dependsOn 'externalNativeBuildRelease'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation files('C:\\Users\\Hassan\\Desktop\\jsch-0.1.55.jar')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //Application Dependencies
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.5'
    implementation 'com.google.android.exoplayer:exoplayer-dash:2.11.5'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.11.5'
    implementation 'com.google.android:flexbox:2.0.1'
}